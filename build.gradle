apply plugin: 'java'

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    jcenter()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.11'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

// This task is an sub class of `Sync`
// `from/into` are methods of `Sync`
task mySync1(type: Sync) {
    println("----- mySync1 -----")
    from "source-dir1"
    from "source-dir2"
    into "target-dir-x"
}

// We can use `project.sync` directly,
// in `doLast` block
task mySync2() {
    doLast {
        println("----- mySync2 -----")
        project.sync {
            from "source-dir1"
            from "source-dir2"
            into "target-dir-y"
        }
    }
}

// This is not working
// `from/into` are just configurations, but if they are in `doFirst/doLast`, they are not run in configuration phase,
// the `Sync` task is not configured actually.
task mySyncNotWorking(type: Sync) {
    println("----- mySyncNotWorking -----")
    doFirst {
        from "source-dir1"
        from "source-dir2"
        into "target-dir-z"
    }
    doLast {
        from "source-dir1"
        from "source-dir2"
        into "target-dir-z"
    }
}
